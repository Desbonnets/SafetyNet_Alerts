plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.5'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'jacoco'
}

group = 'org.example'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.apache.logging.log4j:log4j-api'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.mockito:mockito-core:5.5.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

jacoco {
    toolVersion = "0.8.10" // Version compatible avec JaCoCo
}

tasks.jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        xml.outputLocation = file("$buildDir/jacoco-reports/jacocoTestReport.xml")
        html.required = true
    }
}

tasks.jacocoTestCoverageVerification {
    dependsOn test
    violationRules {
        rule {
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.80
            }
        }
    }
}

tasks.test {
    useJUnitPlatform() // S'assure que JUnit 5 est utilisé
    finalizedBy(tasks.jacocoTestReport) // Générer automatiquement le rapport après les tests
    reports {
        html.required = true // Générer un rapport HTML
        junitXml.required = true // Générer un rapport XML (par défaut pour CI/CD)
        junitXml.outputLocation = file("$buildDir/surefire-reports")
    }
}
